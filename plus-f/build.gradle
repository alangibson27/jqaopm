buildscript {
    dependencies {
        classpath 'com.github.skhatri:gradle-s3-plugin:1.0.4'
        classpath 'joda-time:joda-time:2.4'
    }
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '4.0.2'
    id "org.jetbrains.kotlin.jvm" version "1.2.41"
}

apply plugin: 's3'

repositories {
    maven {
        url "http://clojars.org/repo/"
    }
    mavenCentral()
}

dependencies {
    compile "com.jsyn:jsyn:20170815"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    testCompile "org.easytesting:fest-swing:1.2.1"
    testCompile 'org.scala-lang:scala-library:2.12.4'
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', 'src/main/kotlin']
        }
    }
}

test {
    tags {
        exclude 'UITest'
    }
}

version = '2.0.2'
mainClassName = 'com.socialthingy.plusf.spectrum.ui.PlusF'

shadowJar {
    baseName = 'plus-f'
    classifier = 'all'
    version = version
}

jar {
    manifest {
        attributes(
                'Main-Class': mainClassName,
                'Class-Path': configurations.runtime.collect { it.getName() }.join(' ')
        )
    }
}

task printVersion {
    doLast {
        println project.version
    }
}

startScripts {
    doLast {
        windowsScript.text = windowsScript.text.replace('set JAVA_EXE=java.exe', 'set JAVA_EXE=..\\runtime\\bin\\java.exe')
    }
}
