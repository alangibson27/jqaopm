buildscript {
    dependencies {
        classpath 'com.github.skhatri:gradle-s3-plugin:1.0.4'
        classpath 'joda-time:joda-time:2.4'
    }
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '4.0.0'
    id "org.jetbrains.kotlin.jvm" version "1.2.41"
}

apply plugin: 's3'

repositories {
    mavenCentral()
}

dependencies {
    compile files('lib/jsyn-20171016.jar')
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    testCompile "org.easytesting:fest-swing:1.2.1"
    testCompile 'org.scala-lang:scala-library:2.12.4'
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', 'src/main/kotlin']
        }
    }
}

test {
    tags {
        exclude 'UITest'
    }
}

version = '1.6.1'
mainClassName = 'com.socialthingy.plusf.spectrum.ui.PlusF'

shadowJar {
    baseName = 'plus-f'
    classifier = 'all'
    version = version
}

jar {
    manifest {
        attributes(
                'Main-Class': mainClassName,
                'Class-Path': configurations.runtime.collect { it.getName() }.join(' ')
        )
    }
}

task writeVersionFile {
    def props = new Properties()
    props.setProperty('version', project.version)
    def propsWriter = new FileWriter('src/main/resources/version.properties')
    try {
        props.store(propsWriter, 'plus-f version')
        propsWriter.flush()
    } finally {
        propsWriter.close()
    }

    def bareWriter = new FileWriter('build/version')
    try {
        bareWriter.append(props.getProperty('version'))
        bareWriter.append('\n')
        bareWriter.flush()
    } finally {
        bareWriter.close()
    }
}

task copyDependencies(type: Copy) {
    destinationDir libsDir
    from configurations.runtime
}

void createJavapackagerTask(String platform, boolean embedJre) {
    task "${platform}Package" (type: Exec, dependsOn: [check, assemble, shadowJar]) {
        def paramEmbeddedJRE = embedJre ? [] : ['-Bruntime=']

        workingDir project.projectDir
        commandLine = [
                'javapackager',
                '-deploy',
                '-v',
                '-nosign',
                '-native', platform,
                '-outdir', "${buildDir}/distributions",
                '-outfile', project.name,
                '-name', 'Plus-F',
                '-description', 'ZX Spectrum Emulator with Network Play Capability',
                '-appclass', mainClassName,
                '-srcdir', libsDir,
                '-srcfiles', shadowJar.archiveName,
                "-BappVersion=${project.version}",
                '-BlicenseType=MIT',
                "-BmainJar=${shadowJar.archiveName}"
        ] + paramEmbeddedJRE
    }
}

for (String platform: ["deb", "rpm"]) {
    createJavapackagerTask(platform, true)
}
